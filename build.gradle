plugins {
    id 'java'
    id 'idea'
}

group = project.maven_group
version = project.plugin_version
archivesBaseName = project.archives_base_name

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
}

compileJava {
    options.compilerArgs << '-parameters'
}

configurations {
    // provided configuration defines configurations which should also be included in the jar
    provided
    implementation.extendsFrom(provided)
}

dependencies {
    implementation 'com.github.DuncanRuns:Jingle:7aa06c9319'
    provided('com.github.DuncanRuns:e4mcbiat:60374f1f77') {
        exclude(group: "com.google.code.gson")
        exclude(group: "org.apache.httpcomponents")
        exclude(group: "commons-io")
        exclude(group: "com.formdev")
        exclude(group: "com.jetbrains.intellij.java")
    }
}


processResources {
    // Totally stolen from Fabric
    inputs.property "version", project.version

    filesMatching("jingle.plugin.json") {
        expand "version": project.version
    }
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from configurations.provided.asFileTree.files.collect { zipTree(it) }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', "**/module-info.*", 'me/duncanruns/e4mcbiat/gui/', 'me/duncanruns/e4mcbiat/Main.class'
}
